repositories {
    maven {
        url 'https://oss.sonatype.org/content/groups/public'
    }
}

configurations {
    styles
    locales
}

def today = LocalDate.now()
def month = today.getMonthValue()
def year = today.getYear() - 2000
if (today.getDayOfMonth() == 1) {
    month--
    if (month == 0) {
        year--
        month = 12
    }
}

dependencies {
    styles "org.citationstyles:styles:$year.$month-SNAPSHOT"
    locales "org.citationstyles:locales:$year.$month-SNAPSHOT"
}

configurations.all {
    // always download snapshots
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

task extractStyles(type: Sync) {
    dependsOn configurations.styles
    from {
        configurations.styles.collect { zipTree(it) }
    }
    into "$buildDir/styles/"
}

task extractLocales(type: Sync) {
    dependsOn configurations.locales
    from {
        configurations.locales.collect { zipTree(it) }
    }
    into "$buildDir/locales/"
}

task extractAll(dependsOn: [extractStyles, extractLocales])

defaultTasks 'extractAll'
